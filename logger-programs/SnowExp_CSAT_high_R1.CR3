'Hay Meadow, Marmot Creek, Kananaskis, AB
' INSERT PROPER STATION NAME HERE'
' Program written by Warren Helgason
' Sept. 29, 2005

'Modified by Lindsey Langs on Jan 8th, 2020 for the snowpack ventilation experiment at Fortress Mountain.
'Modified by Angus Duncan on July 4th, 2016 for a roving EC Station at Fortress Mountain.

'Wiring Details

'G:
'SE1 (1H):  KH20_high          WHITE          Signal
'SE2 (1L):  KH20_high          BLACK          Signal Reference
'G:         KH20_high          CLEAR          Sheild
'           KH20_high          Jumper to 1L


'G:         KH20_high          CLEAR          Shield
'P3:
'G:        
'P4:

'C8:
'G:          CSAT3_high         BLACK/CLEAR    Digital ground/shield
'            
'5V:         
'G:          CSAT3_high         CLEAR          Power shield
'G:          
'SW1:
'SW2:
'G:          KH20_high          BLACK          Power ground         
'12V:        KH20_high          RED            Power         
'12V:        CSAT3_high         RED            Power         
'G:          CSAT3_high         BLACK          Power ground
'            

'SDM-C1:     CSAT3_high         GREEN          Data           
'SDM-C2:     CSAT3_high         WHITE          Clock          
'SDM-C3:     CSAT3_high         BROWN          Enable
'            

'****** Constants ******
' Measurement Rate - 20 Hz (10Hz) - change these constants to the values in brackets for 10Hz freq.
'Oct 2 - change to 10Hz rate until program is working
Const SCAN_INTERVAL = 50 ' (100mSec)
Const CSAT_OPT = 20 '(10) - this tells the CSAT what freq. to expect a trigger
Const ANALOG_DELAY = 6 '6 scan delay (4(3 scan delay)) - delay for non-csat flux instruments eg. KH20, FWTC etc.
Const CSAT_DELAY = 4 '(4 scan delay (2(1 scan delay)) - delay for CSAT to match the analog delay... pipeline delay of 2 scans is done automatically by SDM

'Latent heat constants (KH20)
'KH20_low' SN1527
Const XKW_high = -0.181 'sensor specific S/N 1527, scaled, dry vapour range
Const Cp_high = 1005 'approximate heat capacity of air
Const LV_high = 2835 'approx. latent heat of sublimation at 0C
Const RHO_high = 1.204 ' estimate air density

Const x = 1  ' Path length of KH20 (cm)
Const xkw = (-1) * x * XKW_high  'Path Length * Water Vapor absorbtion coefficient (M^3 / g)
Public rho_w
Units rho_w = g/m^3


'Other constants:
'CSAT sensor head orientation
Const ANGLE_FROM_NORTH = -116 'this value is negative when West of North, and positive when East of North
'CSAT SDM clock speed - see CSAT manual
Const SDM_PER = 30


SequentialMode 

'****** Variables ******
' raw (unshifted variables
Dim CSAT_raw(5)
Dim KH20_raw

'shifted variables
Public CSAT_shifted(5)
Alias CSAT_shifted(1) = Ux
Alias CSAT_shifted(2) = Uy
Alias CSAT_shifted(3) = Uz
Alias CSAT_shifted(4) = Ts
Alias CSAT_shifted(5) = diag_word
Public KH20_shifted
Alias KH20_shifted = vh
Public ln_vh

'covariance variables
Public cov_in(5)
Public cov_out(19)
Alias cov_out(1) = Ux_Ux
Alias cov_out(2) = Ux_Uy
Alias cov_out(3) = Ux_Uz
Alias cov_out(4) = Ux_ln_vh
Alias cov_out(5) = Ux_Ts
Alias cov_out(6) = Uy_Uy
Alias cov_out(7) = Uy_Uz
Alias cov_out(8) = Uy_ln_vh
Alias cov_out(9) = Uy_Ts
Alias cov_out(10) = Uz_Uz
Alias cov_out(11) = Uz_ln_vh
Alias cov_out(12) = Uz_Ts
Alias cov_out(13) = ln_vh_ln_vh
Alias cov_out(14) = ln_vh_Ts
Alias cov_out(15) = Ts_Ts

'mean wind variables
Alias cov_out(16) = horiz_wind_spd
Alias cov_out(17) = result_wind_spd
Alias cov_out(18) = wind_dir
Alias cov_out(19) = wind_dir_std_dev
Public wnd_dir_compass
Dim wind_easting
Dim wind_northing

'Flux variables
Public LE
Public Hs
Public tau
Public u_star

'Variables in Slow Sequence... ie. auxiliary met. measurements
Public BattVolt 'measures battery voltage


'diagnostic variables
Public intermed_disable 'disable intermediate processing
Public n(2) 'number of samples in the online covariances
Public CSAT_warnings
Public diag_bits(4)
Alias diag_bits(1) = del_T_f 'delta temperature warning
Alias diag_bits(2) = track_f 'poor signal lock
Alias diag_bits(3) = amp_h_f 'sonic signal amplitude too high
Alias diag_bits(4) = amp_l_f 'sonic signal amplitude too low
Dim hex_number 'used to dissolve the diagnostic word from the CSAT
Public flag(8)

'programming & other variables
Dim scan_count 'counts the number of scans that have been executed
Dim delays_loaded 'True/False depending on no. of scans
Dim j 'counter

'****** Data Tables ******

' Raw wind, sonic temperature, and hygrometer reading (not shifted)
DataTable (raw_comp,TRUE,1)
  Sample (5,CSAT_raw(),IEEE4)
  Sample (1,KH20_raw,IEEE4)
EndTable

'Table of analog measurements that have been shifted
DataTable (alog_shf,TRUE,ANALOG_DELAY)
  Sample (1,KH20_raw,IEEE4)
EndTable

'Table of CSAT measurements that have been shifted
DataTable (CSAT_shf,TRUE,CSAT_DELAY)
  Sample (5,CSAT_raw(),IEEE4)
EndTable

'Table of time-series data (high freq.)
DataTable (highfreq,TRUE,-1)
  DataInterval (0,SCAN_INTERVAL,mSec,100)
  CardOut (0 ,-1)
  Sample (4,CSAT_shifted(),FP2)
  Sample (1,KH20_shifted,FP2)
  Sample (1,diag_word,FP2)
EndTable

'Table of computed covariances
DataTable (comp_cov,TRUE,1)
  DataInterval (0,15,Min,1)
  Covariance (5,cov_in(),IEEE4,(intermed_disable OR NOT (flag(7))),15)
  WindVector (1,wind_easting,wind_northing,IEEE4,(intermed_disable OR NOT (flag(7))),0,1,2)
EndTable

'Table of 5-minute computed fluxes
DataTable (flux15,1,-1)' WHAT SIZE SHOULD THIS BE????
  DataInterval (0,15,Min,10)
  CardOut (0 ,-1)
  Sample (1,LE,IEEE4)
  Sample(1,Hs,IEEE4)
  Sample(1,tau,IEEE4)
  Sample(1,u_star,IEEE4)
  Sample(15,cov_out(),IEEE4)'output all covariances
  Average(3,CSAT_shifted,IEEE4,(intermed_disable OR NOT (flag(7)))) 'average Ux,Uy,Uz
  Average(1,ln_vh,IEEE4,(intermed_disable OR NOT (flag(7))))
  Average(1,Ts,IEEE4,(intermed_disable OR NOT (flag(7))))
  Sample(4,cov_out(16),IEEE4)
  Sample (1,wnd_dir_compass,IEEE4)
  Average(1,vh,FP2,FALSE)
  Totalize (1,n(),FP2,FALSE)
  Totalize (4,diag_bits(),FP2,FALSE)
EndTable



'****** Station Name *****
StationName (FRG)

'****** BEGIN PROGRAM ******
BeginProg
  
  ' Test flags
  flag(7) = True 'maintenance flag

  'Set all CSAT3 variables to NaN?
  For j=1 To 4
    CSAT_raw(j) = NaN
  Next j

  'Set the SDM clock speed
  SDMSpeed (30 )

  Scan (SCAN_INTERVAL,mSec,10,0)

    '*** CSAT measurement ***
    ' get u,v,w,Tsonic,& Diagnostic word
    CSAT3 (CSAT_raw(),1,0,91,CSAT_OPT)

    '*** KH20 measurement ***
    'measure KH20
    VoltDiff (KH20_raw,1,mV5000,1,True,0,250,1.0,0)

    'write the raw turbulence data to raw_component table
    CallTable raw_comp

    'write the data to analog_shift table for delay processing
    CallTable alog_shf

    'write the data to CSAT_shift table for delay processing
    CallTable CSAT_shf

    'check to see if the delays are loaded...
    If (NOT delays_loaded) Then (scan_count = scan_count+1)
    If (scan_count = ANALOG_DELAY) Then (delays_loaded = TRUE)


    'load the data from the delayed analog & CSAT tables
    GetRecord (KH20_shifted,alog_shf,ANALOG_DELAY)
    GetRecord(CSAT_shifted(),CSAT_shf,CSAT_DELAY)
    'take the natural log of the KH20 reading
    ln_vh = LOG(vh)
    'Divide by unique sensor coefficient
    rho_w = (ln_vh / xkw)
    'assign wind eastings and northings
    wind_easting = -1 * Uy
    wind_northing = Ux

    'THE FOLLOWING PROCEDURES USED FOR GETTING DIAG WORD NEED TO BE REVIEWED
    'Turn on the intermediate processing disable flag when the CSAT3 is reporting NaN, a
    'Lost Trigger (&hf000), No Data (&hf03f), or an SDM error (&hf001).
    If ( (diag_word = NaN) OR (diag_word = &hf000) OR (diag_word = &hf03f) OR (diag_word = &hf001))
      intermed_disable = TRUE
    Else
      'Check for any warning flags in CSAT3 data.  Filter all measurements associated
      ' with the CSAT3, when the warning flags are set.
      If (diag_word AND &hf000)
        CSAT_warnings = 1
        intermed_disable = TRUE
      Else
        CSAT_warnings = 0
        intermed_disable = FALSE
      EndIf
    EndIf

    'Keep the four most significant bits of the diagnostic word.
    diag_word = INT ((diag_word AND &hf000)/&h1000 + 0.5)

    'Break down the four most significant bits of the diagnostic word
    ' into a delta temperature flag, poor signal lock (tracking flag),
    ' amplitude high flag, and amplitude low flag.
    hex_number = &h0008
    For j = 1 To 4
      If ( ((diag_word AND hex_number) = hex_number) AND NOT (diag_word = &h000f) )
        diag_bits(j) = 1
      Else
        diag_bits(j) = 0
      EndIf
      If ( diag_word = NaN ) Then ( diag_bits(j) = NaN )
      hex_number = INT ((hex_number/&h0002) + 0.5)
    Next j


    CallTable highfreq

    'Populate the cov_in array for covariance computation
    cov_in(1) = Ux
    cov_in(2) = Uy
    cov_in(3) = Uz
    cov_in(4) = ln_vh
    cov_in(5) = Ts

    CallTable comp_cov

    'Count the number of samples used in the covariances
    If (NOT intermed_disable AND flag(7))
      n(1)=1
    Else
      n(1)=0
    EndIf

    If (comp_cov.Output(1,1))'if data were output last time the table was called, then...
    GetRecord (cov_out,comp_cov,1)
    wnd_dir_compass = wind_dir + ANGLE_FROM_NORTH
    wnd_dir_compass = wnd_dir_compass MOD 360

'compute the on-line fluxes
    LE = LV_high * Uz_ln_vh/xkw
    Hs = RHO_high * Cp_high * Uz_Ts
    tau = RHO_high*SQR((Ux_Uz)^2 + (Uy_Uz)^2)
    u_star = SQR(tau/RHO_high)

  EndIf

  CallTable flux15
NextScan

'****** Beginning of Auxiliary Measurement section

SlowSequence

 



Scan (10,Sec,3,0)
  'Measure Battery Voltage
  Battery (BattVolt)


NextScan

EndProg

